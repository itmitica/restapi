Request ::=
  Request-Line
  (
    ( 
      general-header       
      | request-header         
      | entity-header
    ) CRLF
  )*
  CRLF
  message-body


Request-Line ::= 
  Method SP Request-URI SP HTTP-Version CRLF

Response ::=
  Status-Line
 (
    (
      general-header
      | response-header
      | entity-header
    ) CRLF
  )*
  CRLF
  message-body

Status-Line ::=
  HTTP-Version SP Status-Code SP Reason-Phrase CRLF

Method ::= 
  "OPTIONS"
  | "GET"
  | "HEAD"
  | "POST"
  | "PUT"
  | "DELETE"
  | "TRACE"
  | "CONNECT"
  | extension-method

extension-method ::= token

Request-URI ::=
 "*"
 | absoluteURI
 | abs_path
 | authority

general-header ::=
  Cache-Control
  | Connection
  | Date
  | Pragma
  | Trailer
  | Transfer-Encoding
  | Upgrade
  | Via
  | Warning

request-header ::= 
  Accept
  | Accept-Charset
  | Accept-Encoding
  | Accept-Language
  | Authorization
  | Expect
  | From
  | Host
  | If-Match
  | If-Modified-Since
  | If-None-Match
  | If-Range
  | If-Unmodified-Since
  | Max-Forwards
  | Proxy-Authorization
  | Range
  | Referer
  | TE
  | User-Agent

response-header ::=
  Accept-Ranges
  | Age
  | ETag
  | Location
  | Proxy-Authenticate
  | Retry-After
  | Server
  | Vary
  | WWW-Authenticate

entity-header ::=
  Allow
  | Content-Encoding
  | Content-Language
  | Content-Length
  | Content-Location
  | Content-MD5
  | Content-Range
  | Content-Type
  | Expires
  | Last-Modified
  | extension-header

extension-header ::= message-header

message-body ::=
  entity-body
 | entity-body_encoded_as_per_Transfer-Encoding